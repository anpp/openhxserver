.Title LDRSAV
	.ASECT
	.=0

START:  NOP 
	BR RUN

;	.WORD	6, 0, 12, 0, 16, 0, 22, 0, 26, 0

	.=30
	.WORD   MYEMT, 340

;	.WORD 36, 0

	.=60
	.WORD	KBINT,200
	.WORD	66, 0

	.=70
KBINT:	TST	@#177562
	RTI

	.=76
SAVSIZ:	.BLKW	1

	.=100
	.WORD	TMINT,200
TMINT:	
	RTI


RUN:
	MOV   #LDSTR, R0        ;Вывести надпись "Loading"
	EMT   #351	

	MOV   #001000, R1  ; Адрес куда считывать остаток файла
	MOV   @#76, R2 ; Длина остатка в словах
	ASL   R2          ; Длина остатка в байтах
	MOV   #1000, R3   ;счетчик для каждых 512 байт
1$:	TSTB  @#176570    ; Приемник готов ?
	BPL   1$      ; Нет
	MOVB  @#176572,(R1)+  ; Переслать принятый байт в память
	
	DEC   R3
	BNE   11$
	MOV   #1000, R3
	;Вывести точку на каждый блок
	MOV   #'., R0
	EMT   #341
11$:
	SOB   R2,1$   ; Продолжаем пока не прочитали всё

	MOV   @#000040, startpr ;сохранить, чтоб можно было перезапустить
	MOV   @#000042,stack   

	;копируем диспетчер EMT в верхнюю память
	MOV   #156000, R0 ;по этому адресу будет диспетчер EMT
	MOV   #MYEMT, R1
	MOV   ENDMYEMT, R2
22$:	MOV   (R1)+, (R0)+
	SOB   R2, 22$

	MOV   #156000, @#30

	MOV   stack,SP
	BIS   #100,@#177560
	MTPS  #0
	MOV   startpr,PC ; Запускаем загруженную программу


LDSTR:	.ASCII	"Loading "<200>
.EVEN

;###### Разбор EMT ######
MYEMT:  	
	MOV       R0, SAVR0        ;сохранить параметр EMT
        MOV       (SP),R0         ;адрес возврата в R0
        MOV       -(R0), R0       ;извлечь команду EMT
        BIC       #1,2(SP)        ;установить в PSW C=0
        CMPB      #374,R0         ;ОБРАЩЕНИЕ К ДИСКУ?
EMT374: BEQ	  CY1             ;установить признак ошибки!
        CMPB      #375,R0         ;ОБРАЩЕНИЕ К ДИСКУ?
EMT375: BEQ       CY1             ;установить признак ошибки!
        CMPB      #341,R0         ;ВЫВОД СИМВОЛА?
        BNE       NE341           ;нет
EMT341: TSTB      @#177564        ;готовность есть?
        BPL       CY1             ;дисплей не готов!
OUTSYM: MOV       SAVR0,R0        ;восстановить R0
        MOVB      R0,@#177566     ;вывести символ
        RTI                       ;выход
NE341:  CMPB      #340,R0         ;ВВОД СИМВОЛА?
EMT340: BNE       NE340           ;нет
        TSTB      @#177560        ;готовность есть?
        BPL       CY1             ;символ не готов!
INSYM:  MOVB      @#177562,R0     ;прочитать символ	
        RTI                       ;выход
CY1:    MOV       SAVR0,R0        ;восстановить R0
        INC       2(SP)           ;установить в PSW C=1
        RTI                       ;и выйти с признаком ошибки

NE340:  CMPB	#351, R0
EMT351: BNE	NOT351
	MOV     SAVR0, R0        ;восстановить R0 (адрес строки) 
2$:	TSTB	@R0
	BEQ	4$
	CMPB	@R0, #200
	BEQ	exit351
3$:     TSTB      @#177564        ;готовность есть?
        BPL       3$             ;дисплей не готов!	
        MOVB   (R0)+,@#177566     ;вывести символ	
	BR	2$
        ;конец строки
4$:	
	TSTB      @#177564        ;готовность есть?
	BPL 4$                  
	MOVB      #15, @#177566    ;вывести символ CR
5$:	
	TSTB      @#177564        ;готовность есть?
	BPL 5$
	MOVB      #12, @#177566   ;вывести символ LF
exit351:
        RTI                       ;выход

NOT351:	CMPB      #354,R0         ;ВЫДЕЛИТЬ ПАМЯТЬ?
        BNE       NE354           ;нет
EMT354: MOV       SAVR0,R0        ;требуемый адрес
        BIC       #1,R0           ;сделать четным
        CMP       #177776,R0      ;максимум памяти?
        BEQ       MAX
        CMP	  HIMEM,R0
        BCC       OK              ;да
MAX:    MOV       HIMEM,R0        ;установить максимум
OK:     MOV       R0,@#50         ;записать адрес в ячейку 50	
        RTI                       ;выход
NE354:  CMPB      #350,R0         ;ВЫХОД В ОС?
	BNE	NE350           ;нет
EMT350: 	
	MOV       stack,SP        ;восстановить стек для программы
        MOV       startpr, PC          ;к перезапуску программы
NE350:  .WORD 0                   ;ОСТАНОВ ПО НЕПРЕДУСМОТРЕННО-
                                  ;МУEMT
        BR        CY1             ;установить признак ошибки!

stack: .word 0
startpr: .word 0
SAVR0: .WORD 0
HIMEM: .WORD 140000

ENDMYEMT: .WORD <ENDMYEMT - MYEMT> / 2

.END START
